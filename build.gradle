// https://docs.gradle.org/current/userguide/building_java_projects.html#sec:java_packaging
plugins {
    id 'java'
    // For source code formatting
    id "com.diffplug.spotless" version "7.2.1"
	// https://github.com/harbby/gradle-serviceloader
    id "com.github.harbby.gradle.serviceloader" version "1.1.9"
	// Shadow
	id "com.gradleup.shadow" version "9.3.0"
    // Download task
    id "de.undercouch.download" version "5.6.0"
}

/**
 * Project Properties
 */
sourceCompatibility = jdkVersion
targetCompatibility = jdkVersion
ext {
	buildID = System.getenv( 'BUILD_ID' ) ?: '0'
	branch = System.getenv( 'BRANCH' ) ?: 'development'
}

if ( branch == 'development' ) {
    // If the branch is 'development', ensure the version ends with '-snapshot'
    // This replaces any existing prerelease identifier with '-snapshot'
    version = version.contains('-') ? version.replaceAll(/-.*/, '-snapshot') : "${version}-snapshot"
	boxlangVersion = boxlangVersion.contains('-') ? boxlangVersion.replaceAll(/-.*/, '-snapshot') : "${boxlangVersion}-snapshot"
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {

	// BoxLang Dependencies
	compileOnly "io.boxlang:boxlang:1.5.0"
	testImplementation "io.boxlang:boxlang:1.5.0"
	// Uncomment only for local testing
	// compileOnly files( '../../boxlang/build/libs/boxlang-' + boxlangVersion + '.jar' )
	// testImplementation files( '../../boxlang/build/libs/boxlang-' + boxlangVersion + '.jar'  )
	// Test Frameworks
	testImplementation "org.junit.jupiter:junit-jupiter:5.+"
	testImplementation "org.mockito:mockito-core:5.+"
	testImplementation "com.google.truth:truth:1.+"
	// Explicitly declare the JUnit platform launcher (to avoid deprecation)
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

java {
    // Produces a javadocs jar
	withJavadocJar()
}

compileJava {
	// Compiler Options
	options.incremental = true
	options.encoding = 'UTF-8'
	options.debug()
}

compileTestJava{
	// Make sure the service loaders are created before testing
	dependsOn compileJava, serviceLoaderBuild
}

/**
 * Clean up
 */
clean {
	doLast{
		var userDir = file( "${System.getProperty('user.home')}/.boxlang/classes" )
		if ( userDir.exists() ) {
			userDir.deleteDir()
			println "+ Cleared user home classes " + userDir.toString()
		}
		println "+ Clean finalized!"
	}
}

javadoc {
    // To avoid our own doclet issues
	options.addBooleanOption( "Xdoclint:none", true )
	//exclude '**/boxlang/parser/**'
	options.addBooleanOption( 'html5', true )
}
task zipJavadocs( type: Zip ) {
	group "documentation"
	from javadoc.destinationDir
	archiveFileName = "${project.name}-javadocs-${version}.zip"
	destinationDirectory = file( "$buildDir/distributions" )
	// Output that the docs have been zippped
	doLast {
		println "+ Javadocs have been zipped to the distribution folder"
	}
}
javadoc.finalizedBy( zipJavadocs )

/**
 * Docs are here:
 * - https://github.com/harbby/gradle-serviceloader,
 * - https://plugins.gradle.org/plugin/com.github.harbby.gradle.serviceloader
 * This generates the META-INF/services files for the ServiceLoader as part of the `build` task
 */
serviceLoader {
    serviceInterface 'ortus.boxlang.runtime.bifs.BIF'
    serviceInterface 'ortus.boxlang.runtime.components.Component'
    serviceInterface 'ortus.boxlang.runtime.async.tasks.IScheduler'
    serviceInterface 'ortus.boxlang.runtime.cache.providers.ICacheProvider'
	serviceInterface 'ortus.boxlang.runtime.events.IInterceptor'
}

jar {
    archiveVersion =  "${version}"
 	manifest {
	   attributes 'Description': 'ExampleBoxLangModule'
    }
}
shadowJar {
    archiveClassifier = ''
    mergeServiceFiles()
    destinationDirectory = file( "build/libs" )
}

/**
 * Source Code Formatting
 */
spotless {
    java {
		target fileTree( "." ) {
            include "**/*.java"
            exclude "**/build/**", "bin/**", "examples/**"
        }
        eclipse().configFile( "./.ortus-java-style.xml" )
		toggleOffOn()
    }
}

/**
 * Custom task that creates a build/module folders
 * - Copies the build/libs/BoxLangModuleTemplate-1.0.0.jar to build/module/libs/BoxLangModuleTemplate-1.0.0.jar
 * - Copies the src/main/bx/** to build/module/ folder.
 */
task createModuleStructure(type: Copy) {
    dependsOn jar
    from( 'build/libs' ) {
		// Note: I have no idea why gradle still adds the -all to the .jar in this directory
		include "${project.name}-${version}.jar"
        into 'libs'
    }
    from( 'src/main/bx' ) {
		include '**/*.bx'
		include '**/*.cfc'
		include '**/*.cfm'
		// Token Replacements Go Here
		filter{ line -> line.replaceAll( '@build.version@', project.version ) }
		if( project.branch == "development" ){
			filter{ line -> line.replaceAll( '\\+@build.number@', '' ) }
		} else {
			filter{ line -> line.replaceAll( '@build.number@', project.buildID ) }
		}
    }
    from( '.' ) {
        include 'box.json'
		include 'readme.md'
		include 'changelog.md'
		// Token Replacements Go Here
		filter{ line -> line.replaceAll( '@build.version@', project.version ) }
		if( project.branch == "development" ){
			filter{ line -> line.replaceAll( '\\+@build.number@', '' ) }
		} else {
			filter{ line -> line.replaceAll( '@build.number@', project.buildID ) }
		}
    }

    destinationDir = file( 'build/module' )
}
task zipModuleStructure( type: Zip ) {
    group "distribution"
    from createModuleStructure.destinationDir
    archiveFileName = "${project.name}-${version}.zip"
    destinationDirectory = file( "$buildDir/distributions" )
    // Output that the module has been zippped
    doLast {
        println "+ Module has been zipped to the distribution folder"
    }
}
createModuleStructure.finalizedBy( zipModuleStructure )
shadowJar.finalizedBy( createModuleStructure )

test {
    useJUnitPlatform()
    testLogging {
		showStandardStreams = true
	}
	//classpath = classpath.filter { !it.path.contains( "build${File.separator}resources" ) }
}

/**
 * TEMPORARY until we publish to maven
 * Task to download the latest jar from https://ortus-temp.s3.amazonaws.com/jericho/libs/boxlang-1.0.0.jar
 */
task downloadBoxLang( type: Download ) {
	// Create the destination directory
	doFirst {
		file( "src/test/resources/libs" ).mkdirs()
	}
    // Configure the URL of the file to download
    src "https://downloads.ortussolutions.com/ortussolutions/boxlang/${boxlangVersion}/boxlang-${boxlangVersion}.jar"
    // Specify the destination directory for the downloaded file
    dest "src/test/resources/libs/boxlang-${boxlangVersion}.jar"
    overwrite true
    onlyIfModified false
}

/**
 * Task to download the BoxLang Doc Generator
 * This is a temporary task until we publish it to maven
 */
task downloadDocGenerator( type: Download ) {
	// Create the destination directory
	doFirst {
		file( "src/test/resources/libs" ).mkdirs()
	}
    // Configure the URL of the file to download
    src "https://downloads.ortussolutions.com/ortussolutions/boxlang-runtimes/boxlang-doc-generator/1.0.0-snapshot/boxlang-doc-generator-1.0.0-snapshot.jar"
    // Specify the destination directory for the downloaded file
    dest "src/test/resources/libs/boxlang-doc-generator-1.0.0-snapshot.jar"
    overwrite true
    onlyIfModified false
}

/**
 * Generate/Scaffold technical documentation
 */
task generateTechnicalDocumentation( type: Javadoc ) {
	source = sourceSets.main.java
	dependsOn downloadDocGenerator, compileJava, serviceLoaderBuild
	// Uncomment and comment the above for local build testing of the doc generator
	// dependsOn compileJava, serviceLoaderBuild
	def classPaths = new ArrayList<File>( sourceSets.main.runtimeClasspath.getFiles() )
	classPaths.addAll( sourceSets.main.java.getFiles() )
	classPaths.addAll( files( 'src/test/resources/libs/boxlang-' + boxlangVersion + '.jar' ) )
	classPaths.addAll( files( 'src/test/resources/libs/boxlang-doc-generator-1.0.0-snapshot.jar' ) )
	options.classpath( classPaths )
	options.addBooleanOption( "Xdoclint:none", true )
	exclude '**/boxlang/parser/**'
	// exclude '**/boxlang/runtime/**'
	options.addBooleanOption( 'html5', true )
	options.doclet = "ortus.boxlang.tools.doclets.BoxLangDoclet"
    options.docletpath = classPaths
}

/**
 * Project Wide Helper function
 * This is not a task, but a reusable UDF
 */
project.ext.bumpVersion = { boolean major = false, boolean minor = false, boolean patch = false ->

	def propertiesFile = file( './gradle.properties' );
	def properties = new Properties();

	properties.load( propertiesFile.newDataInputStream() )
	def versionTarget = major ? 0 : minor ? 1 : 2

	def currentVersion = properties.getProperty( 'version' )
	def versionParts = currentVersion.split( '\\.' )
	def newPathVersion = versionParts[ versionTarget ].toInteger() + 1
	def newVersion = '';

	if( patch ){
		newVersion = "${versionParts[ 0 ]}.${versionParts[ 1 ]}.${newPathVersion}"
	} else if( minor ){
		newVersion = "${versionParts[ 0 ]}.${newPathVersion}.${versionParts[ 2 ]}"
	} else if( major ){
		newVersion = "${newPathVersion}.${versionParts[ 1 ]}.${versionParts[ 2 ]}"
	}

	properties.setProperty( 'version', newVersion )
	properties.store( propertiesFile.newWriter(), null )

	println "Bumped version from ${currentVersion} to ${newVersion}"
}
/**
 * Bump the major version number
 */
task bumpMajorVersion {
	doLast{
		bumpVersion( true )
	}
}

/**
 * Bump the minor version number
 */
task bumpMinorVersion {
	doLast{
		bumpVersion( false, true )
	}
}

/**
 * Bump the patch version number
 */
task bumpPatchVersion {
	doLast{
		bumpVersion( false, false, true )
	}
}
